************************************************************
***       Fig. 6:  SET - Ring Oscillator                 ***
************************************************************

.param temperature=10
Vbias 10 0 PWL (0, 0) (2n, 0.030) (350n, 0.030V)     ;  positive bias voltage

Cp1  11 0 1E-13
Cp2  12 0 1.2E-13
Cp3  13 0 .9E-13
Rp1  10 11 1E3
Rp2  11 12 1E3
Rp3  12 13 1E3


Vdrain 2 0 0
Vg 16 0 0          ;  gate voltage

XInverter1 11 2 5 3 16 Inverter1
XInverter2 12 2 3 4 16 Inverter1
XInverter3 13 2 4 5 16 Inverter1
*XInverter4 1 2 5 6 16 Inverter

*Vin 15 0 SFFM (0.02 0.02 2GHz 0 0)       ;  input voltage



.SUBCKT Inverter1 1 2 3 4 5    ; source drain input output gate2
Xset1 1 6 3 5  SET PARAMS:         ;  source drain gate1 gate2
    +C1=1E-18                      ;  Capacitance of junction 1 [F]
    +C2=1E-18                      ;  Capacitance of junction 2 [F]
    +R1=100E6                        ;  Resistance of junction 1 [Ohm]
    +R2=100E6                        ;  Resistance of junction 2 [Ohm]
    +Cg1=2E-18                     ;  Capacitance of gate 1 [F]
    +Cg2=0                         ;  Capacitance of gate 2 [F]
    +Q0=-0.15                        ;  Offset charge [e]
    +TEMP={temperature}            ;  Temperature [K]
Xset2 6 2 3 5  SET PARAMS:         ;  source drain gate1 gate2
    +C1=1E-18                      ;  Capacitance of junction 1 [F]
    +C2=1E-18                      ;  Capacitance of junction 2 [F]
    +R1=100E6                        ;  Resistance of junction 1 [Ohm]
    +R2=100E6                        ;  Resistance of junction 2 [Ohm]
    +Cg1=2E-18                     ;  Capacitance of gate 1 [F]
    +Cg2=0                         ;  Capacitance of gate 2 [F]
    +Q0=0.15                       ;  Offset charge [e]
    +TEMP={temperature}            ;  Temperature [K]
Rload 6 0 1000T                      ;  Load Resistance
Cload 6 0 1E-16                    ;  Load Capacitance
Rline 6 4 10000000
Cline 4 0 1E-16

.ENDS Inverter1



.SUBCKT Inverter2 1 2 3 4 5    ; source drain input output gate2
Xset1 1 6 3 5  SET PARAMS:         ;  source drain gate1 gate2
    +C1=1E-18                      ;  Capacitance of junction 1 [F]
    +C2=1E-18                      ;  Capacitance of junction 2 [F]
    +R1=100E6                        ;  Resistance of junction 1 [Ohm]
    +R2=100E6                        ;  Resistance of junction 2 [Ohm]
    +Cg1=2E-18                     ;  Capacitance of gate 1 [F]
    +Cg2=0                         ;  Capacitance of gate 2 [F]
    +Q0=-0.15                        ;  Offset charge [e]
    +TEMP={temperature}            ;  Temperature [K]
Xset2 6 2 3 5  SET PARAMS:         ;  source drain gate1 gate2
    +C1=1E-18                      ;  Capacitance of junction 1 [F]
    +C2=1E-18                      ;  Capacitance of junction 2 [F]
    +R1=100E6                        ;  Resistance of junction 1 [Ohm]
    +R2=100E6                        ;  Resistance of junction 2 [Ohm]
    +Cg1=2E-18                     ;  Capacitance of gate 1 [F]
    +Cg2=0                         ;  Capacitance of gate 2 [F]
    +Q0=0.15                       ;  Offset charge [e]
    +TEMP={temperature}            ;  Temperature [K]
Rload 6 0 1000T                      ;  Load Resistance
Cload 6 0 1E-16                    ;  Load Capacitance
Rline 6 4 130000000
Cline 4 0 1E-16
.ENDS Inverter2


*single electron transistor
*connections: source
*             | drain
*             | | gate1
*             | | | gate2
*             | | | |
.SUBCKT SET   1 2 3 4 PARAMS:
    +C1=1E-18                      ;  Capacitance of junction 1
    +C2=1E-18                      ;  Capacitance of junction 2
    +R1=100E6                        ;  Resistance of junction 1
    +R2=100E6                        ;  Resistance of junction 2
    +Cg1=1E-18                     ;  Capacitance of gate 1
    +Cg2=0                         ;  Capacitance of gate 2
    +C0=0                          ;  Self Capacitance of the island
    +Q0=0                          ;  Offset charge in units of e
    +TEMP=4.2                      ;  Temperature

.PARAM PI=3.1415926535897932       ;  Pi constant
.PARAM E=1.60217733E-19            ;  Electronic charge
.PARAM KB=1.380658E-23             ;  Boltzmann's constant
.PARAM CSUM={C1+C2+Cg1+Cg2+C0}     ;  The total capacitance of the SET
.PARAM T={TEMP*CSUM*5.3785467E14}  ;  Normalized temperature 5.3785467E14 = kB/e^2
.PARAM RN1={R1/(R1+R2)}            ;  Normalized resistance of junction 1
.PARAM RN2={R2/(R1+R2)}            ;  Normalized resistance of junction 2

.FUNC Q(v1,v2,v3,v4) { (Cg1*v3+Cg2*v4+C1*v1+C2*v2)/E+Q0 }           ;  Definition of a charge term in units of e
.FUNC VN(v) { CSUM*v/E }                                            ;  The normalized voltage
.FUNC GAMMA(u) { IF(T==0,IF(u<0,-u,0),IF(u==0,T,u/(EXP(u/T)-1)))}   ;  The rate function
.FUNC ROUND(x) { x-IF(cos(PI*x)>0,arcsin(sin(PI*x))/PI,-arcsin(sin(PI*x))/PI) } ;  The round() function
.FUNC N_OPT(v1,v2,v3,v4) { ROUND(-Q(v1,v2,v3,v4)+(CSUM/E)*(v1*RN2+v2*RN1)) }    ;  The most probable charge on the island in units of e

*************************** the rates for the four tunnel events*****************************

.FUNC R1L(n,v1,v2,v3,v4) {GAMMA(0.5 - n - Q(v1,v2,v3,v4) + VN(v1))/RN1}
.FUNC R1R(n,v1,v2,v3,v4) {GAMMA(0.5 + n + Q(v1,v2,v3,v4) - VN(v1))/RN1}
.FUNC R2L(n,v1,v2,v3,v4) {GAMMA(0.5 + n + Q(v1,v2,v3,v4) - VN(v2))/RN2}
.FUNC R2R(n,v1,v2,v3,v4) {GAMMA(0.5 - n - Q(v1,v2,v3,v4) + VN(v2))/RN2}

* determine the relative probabilities; charge state N_OPT is initially assumed to have a relative probability equal to one

.FUNC PN_1(n,v1,v2,v3,v4) {(R1L(n,v1,v2,v3,v4)+R2R(n,v1,v2,v3,v4))/(R1R(n-1,v1,v2,v3,v4)+R2L(n-1,v1,v2,v3,v4))}
*.FUNC PN_2(n,v1,v2,v3,v4) { PN_1(n,v1,v2,v3,v4)*
*                            +(R1L(n-1,v1,v2,v3,v4)+R2R(n-1,v1,v2,v3,v4))/(R1R(n-2,v1,v2,v3,v4)+R2L(n-2,v1,v2,v3,v4))}
*.FUNC PN_3(n,v1,v2,v3,v4) { PN_2(n,v1,v2,v3,v4)*
     *                       +(R1L(n-2,v1,v2,v3,v4)+R2R(n-2,v1,v2,v3,v4))/(R1R(n-3,v1,v2,v3,v4)+R2L(n-3,v1,v2,v3,v4))}
*.FUNC PN_4(n,v1,v2,v3,v4) { PN_3(n,v1,v2,v3,v4)*
      *                      +(R1L(n-3,v1,v2,v3,v4)+R2R(n-3,v1,v2,v3,v4))/(R1R(n-4,v1,v2,v3,v4)+R2L(n-4,v1,v2,v3,v4))}
*.FUNC PN_5(n,v1,v2,v3,v4) { PN_4(n,v1,v2,v3,v4)*
*                            +(R1L(n-4,v1,v2,v3,v4)+R2R(n-4,v1,v2,v3,v4))/(R1R(n-5,v1,v2,v3,v4)+R2L(n-5,v1,v2,v3,v4))}
.FUNC PN1(n,v1,v2,v3,v4) {(R2L(n,v1,v2,v3,v4)+R1R(n,v1,v2,v3,v4))/(R2R(n+1,v1,v2,v3,v4)+R1L(n+1,v1,v2,v3,v4))}
*.FUNC PN2(n,v1,v2,v3,v4) { PN1(n,v1,v2,v3,v4)*
*                           +(R2L(n+1,v1,v2,v3,v4)+R1R(n+1,v1,v2,v3,v4))/(R2R(n+2,v1,v2,v3,v4)+R1L(n+2,v1,v2,v3,v4))}
*.FUNC PN3(n,v1,v2,v3,v4) { PN2(n,v1,v2,v3,v4)*
  *                         +(R2L(n+2,v1,v2,v3,v4)+R1R(n+2,v1,v2,v3,v4))/(R2R(n+3,v1,v2,v3,v4)+R1L(n+3,v1,v2,v3,v4))}
*.FUNC PN4(n,v1,v2,v3,v4) { PN3(n,v1,v2,v3,v4)*
         *                  +(R2L(n+3,v1,v2,v3,v4)+R1R(n+3,v1,v2,v3,v4))/(R2R(n+4,v1,v2,v3,v4)+R1L(n+4,v1,v2,v3,v4))}
*.FUNC PN5(n,v1,v2,v3,v4) { PN4(n,v1,v2,v3,v4)*
  *                         +(R2L(n+4,v1,v2,v3,v4)+R1R(n+4,v1,v2,v3,v4))/(R2R(n+5,v1,v2,v3,v4)+R1L(n+5,v1,v2,v3,v4))}

.FUNC PSUM(n,v1,v2,v3,v4) { PN_1(n,v1,v2,v3,v4)+1+PN1(n,v1,v2,v3,v4)}
* PN_4(n,v1,v2,v3,v4)+PN_3(n,v1,v2,v3,v4)+PN_2(n,v1,v2,v3,v4)
*                          ++PN_1(n,v1,v2,v3,v4)+1+PN1(n,v1,v2,v3,v4)+PN2(n,v1,v2,v3,v4)+PN3(n,v1,v2,v3,v4)
*                          ++PN4(n,v1,v2,v3,v4)+PN5(n,v1,v2,v3,v4) }

**************** calculate the current from source to drain ****************

.FUNC CUR(n,v1,v2,v3,v4) { PN_1(n,v1,v2,v3,v4)*(R1R(n-1,v1,v2,v3,v4)-R1L(n-1,v1,v2,v3,v4))+(R1R(n,v1,v2,v3,v4)-R1L(n,v1,v2,v3,v4))+PN1(n,v1,v2,v3,v4)*(R1R(n+1,v1,v2,v3,v4)-R1L(n+1,v1,v2,v3,v4))}


*PN_5(n,v1,v2,v3,v4)*(R1R(n-5,v1,v2,v3,v4)-R1L(n-5,v1,v2,v3,v4))
                          *++PN_4(n,v1,v2,v3,v4)*(R1R(n-4,v1,v2,v3,v4)-R1L(n-4,v1,v2,v3,v4))
                          *++PN_3(n,v1,v2,v3,v4)*(R1R(n-3,v1,v2,v3,v4)-R1L(n-3,v1,v2,v3,v4))
                          *++PN_2(n,v1,v2,v3,v4)*(R1R(n-2,v1,v2,v3,v4)-R1L(n-2,v1,v2,v3,v4))
                          *++PN_1(n,v1,v2,v3,v4)*(R1R(n-1,v1,v2,v3,v4)-R1L(n-1,v1,v2,v3,v4))
                          *++(R1R(n,v1,v2,v3,v4)-R1L(n,v1,v2,v3,v4))}
                          *++PN1(n,v1,v2,v3,v4)*(R1R(n+1,v1,v2,v3,v4)-R1L(n+1,v1,v2,v3,v4))
                          *++PN2(n,v1,v2,v3,v4)*(R1R(n+2,v1,v2,v3,v4)-R1L(n+2,v1,v2,v3,v4))
                          *++PN3(n,v1,v2,v3,v4)*(R1R(n+3,v1,v2,v3,v4)-R1L(n+3,v1,v2,v3,v4))
                          *++PN4(n,v1,v2,v3,v4)*(R1R(n+4,v1,v2,v3,v4)-R1L(n+4,v1,v2,v3,v4))
                          *++PN5(n,v1,v2,v3,v4)*(R1R(n+5,v1,v2,v3,v4)-R1L(n+5,v1,v2,v3,v4)) }

.FUNC CURRENT(n,v1,v2,v3,v4) { E*CUR(n,v1,v2,v3,v4)/(CSUM*PSUM(n,v1,v2,v3,v4)*(R1+R2)) }

********************** calculate the island voltage ************************

.FUNC VOLT(n,v1,v2,v3,v4) { PN_1(n,v1,v2,v3,v4)*(n-1+Q(v1,v2,v3,v4))+n+Q(v1,v2,v3,v4)+PN1(n,v1,v2,v3,v4)*(n+1+Q(v1,v2,v3,v4))}

*PN_5(n,v1,v2,v3,v4)*(n-5+Q(v1,v2,v3,v4))
                           *++PN_4(n,v1,v2,v3,v4)*(n-4+Q(v1,v2,v3,v4))
                           *++PN_3(n,v1,v2,v3,v4)*(n-3+Q(v1,v2,v3,v4))
                           *++PN_2(n,v1,v2,v3,v4)*(n-2+Q(v1,v2,v3,v4))
                           *++PN_1(n,v1,v2,v3,v4)*(n-1+Q(v1,v2,v3,v4))
                           *++n+Q(v1,v2,v3,v4)}
                           *++PN1(n,v1,v2,v3,v4)*(n+1+Q(v1,v2,v3,v4))
                           *++PN2(n,v1,v2,v3,v4)*(n+2+Q(v1,v2,v3,v4))
                           *++PN3(n,v1,v2,v3,v4)*(n+3+Q(v1,v2,v3,v4))
                           *++PN4(n,v1,v2,v3,v4)*(n+4+Q(v1,v2,v3,v4))
                           *++PN5(n,v1,v2,v3,v4)*(n+5+Q(v1,v2,v3,v4)) }

.FUNC VOLTAGE(n,v1,v2,v3,v4) { (E/CSUM)*VOLT(n,v1,v2,v3,v4)/PSUM(n,v1,v2,v3,v4) }

E1 5 0 VALUE={VOLTAGE(N_OPT(V(1),V(2),V(3),V(4)),V(1),V(2),V(3),V(4))}     ;  Voltage of the island
G1 1 2 VALUE={CURRENT(N_OPT(V(1),V(2),V(3),V(4)),V(1),V(2),V(3),V(4))}     ;  Current from source to drain
CT1 1 5 {C1}
CT2 2 5 {C2}
CGATE1 3 5 {CG1}
CGATE2 4 5 {CG2}
.ENDS SET


.OPTIONS VNTOL=1E-11 ABSTOL 1E-19 GMIN 1E-19 RELTOL 0.01 ITL1 300

.IC V(3)=0.0 V(4)=0.0 V(5)=0.0
.TRAN 55E-3ns 20000ns 0us 5E-2ns SKIPBP

*.DC Vin -0. 0.025 0.00025
*.STEP PARAM temperature LIST 4.2

.PRINT TRAN V(3) V(4) V(5)
.PROBE
.END


